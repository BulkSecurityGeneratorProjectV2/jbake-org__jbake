import java.time.format.DateTimeFormatter

plugins {
    id "org.jbake.convention.java-common"
    id 'java-library'
    id 'org.ajoberstar.grgit' version "$grgitVersion"
}

apply from: "$rootDir/gradle/maven-publishing.gradle"
apply from: "$rootDir/gradle/signing.gradle"

description = "The core library of JBake"

dependencies {
    api "commons-io:commons-io:$commonsIoVersion"
    api "org.apache.commons:commons-configuration2:$commonsConfigurationVersion"
    implementation "commons-beanutils:commons-beanutils:$commonsBeanutilsVersion"
    implementation "org.apache.commons:commons-vfs2:$commonsVfs2Version"
    implementation "org.apache.commons:commons-lang3:$commonsLangVersion"
    implementation("com.googlecode.json-simple:json-simple:$jsonSimpleVersion") {
        exclude group: "junit", module: "junit"
    }
    implementation "com.orientechnologies:orientdb-core:$orientDbVersion"
    api "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
    api "org.codehaus.groovy:groovy:$groovyVersion"
    api "org.codehaus.groovy:groovy-templates:$groovyVersion"
    api "org.codehaus.groovy:groovy-dateutil:$groovyVersion"
    api "org.freemarker:freemarker:$freemarkerVersion"
    api "org.thymeleaf:thymeleaf:$thymeleafVersion"
    api "de.neuland-bfi:jade4j:$jade4jVersion"
    api "com.vladsch.flexmark:flexmark:$flexmarkVersion"
    api "com.vladsch.flexmark:flexmark-profile-pegdown:$flexmarkVersion"
    api "io.pebbletemplates:pebble:$pebbleVersion"
    implementation "org.jsoup:jsoup:$jsoupVersion"
    implementation "org.yaml:snakeyaml:$snakeYamlVersion"

    // cli specific dependencies
    implementation "org.eclipse.jetty:jetty-server:$jettyServerVersion"
    implementation "info.picocli:picocli:$picocli"
}

processResources {
    from("src/main/resources") {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        include 'default.properties'
        expand jbakeVersion: project.version,
               timestamp: grgit.head().dateTime.format(DateTimeFormatter.ofPattern("yyyy-MM-dd HH:mm:ss'['VV']'")),
               gitHash: grgit.head().abbreviatedId
    }
}
